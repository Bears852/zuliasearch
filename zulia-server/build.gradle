
apply plugin: 'application'

description 'Zulia Server'


def luceneVersion = '6.6.0'


defaultTasks 'build','installDist'

dependencies {
    compile project(':zulia-common')

    compile "org.apache.lucene:lucene-facet:$luceneVersion"
    compile "org.apache.lucene:lucene-queryparser:$luceneVersion"
    compile "org.apache.lucene:lucene-analyzers-common:$luceneVersion"
    compile "org.apache.lucene:lucene-highlighter:$luceneVersion"

    compile 'com.hazelcast:hazelcast:3.8.2'

    compile 'com.beust:jcommander:1.72'

    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.25.1'

    compile 'com.cedarsoftware:json-io:4.9.12'

    compile 'info.debatty:java-lsh:0.10'

    compile 'org.mongodb:mongo-java-driver:3.4.2'

}

applicationName = 'zulia'
mainClassName = "io.zulia.server.util.Zulia"

task createZuliaDScript(type: CreateStartScripts) {
    applicationName = "zuliad"
    mainClassName = "io.zulia.server.ZuliaD"
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath

    doLast {
        def unixScriptFile = file getUnixScript()
        unixScriptFile.text = unixScriptFile.text.replace('APP_HOME="`pwd -P`"', 'export APP_HOME="`pwd -P`"')
    }
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    from(createZuliaDScript)
    fileMode = 0755
}

