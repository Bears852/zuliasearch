apply plugin: 'application'

apply from: '../maven.gradle'

description 'Zulia Server'


def luceneVersion = '7.3.0'
def micronautVersion = '1.0.0.M1'


defaultTasks 'build', 'installDist'

dependencies {
    compile project(':zulia-client')

    // Lucene
    compile "org.apache.lucene:lucene-facet:$luceneVersion"
    compile "org.apache.lucene:lucene-queryparser:$luceneVersion"
    compile "org.apache.lucene:lucene-analyzers-common:$luceneVersion"
    compile "org.apache.lucene:lucene-highlighter:$luceneVersion"

    // Micronaut
    compile "io.reactivex.rxjava2:rxjava:2.1.14"
    annotationProcessor "io.micronaut:inject-java:$micronautVersion"
    compile "io.micronaut:http-client:$micronautVersion"
    compile "io.micronaut:http-server-netty:$micronautVersion"
    compile "io.micronaut:inject:$micronautVersion"
    compile "io.micronaut:runtime:$micronautVersion"
    compileOnly "io.micronaut:inject-java:$micronautVersion"
    //testCompile "junit:junit:4.12"
    //testCompile "io.micronaut:inject-java:$micronautVersion"

    // Other
    compile 'com.beust:jcommander:1.72'

    compile 'com.cedarsoftware:json-io:4.9.12'

    compile 'info.debatty:java-lsh:0.10'

    compile 'org.mongodb:mongodb-driver:3.6.3'

}

applicationName = 'zulia'
mainClassName = "io.zulia.server.cmd.Zulia"

task createZuliaDScript(type: CreateStartScripts) {
    applicationName = "zuliad"
    mainClassName = "io.zulia.server.cmd.ZuliaD"
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath

    doLast {
        def unixScriptFile = file getUnixScript()
        unixScriptFile.text = unixScriptFile.text.replace('APP_HOME="`pwd -P`"', 'export APP_HOME="`pwd -P`"')
    }
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createZuliaDScript)
    fileMode = 0755
}

compileJava.options.compilerArgs += '-parameters'
//compileTestJava.options.compilerArgs += '-parameters'
