apply plugin: 'application'

apply from: '../maven.gradle'

description 'Zulia Server'


def luceneVersion = '7.2.1'


defaultTasks 'build', 'installDist'

dependencies {
    compile project(':zulia-common')
    compile project(':zulia-client') //needed for admin tools

    compile "org.apache.lucene:lucene-facet:$luceneVersion"
    compile "org.apache.lucene:lucene-queryparser:$luceneVersion"
    compile "org.apache.lucene:lucene-analyzers-common:$luceneVersion"
    compile "org.apache.lucene:lucene-highlighter:$luceneVersion"

    compile 'com.beust:jcommander:1.72'

    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.25'



    compile 'com.cedarsoftware:json-io:4.9.12'

    compile 'info.debatty:java-lsh:0.10'

    compile 'org.mongodb:mongodb-driver:3.6.1'

}

applicationName = 'zulia'
mainClassName = "io.zulia.server.cmd.Zulia"

task createZuliaDScript(type: CreateStartScripts) {
    applicationName = "zuliad"
    mainClassName = "io.zulia.server.cmd.ZuliaD"
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath

    doLast {
        def unixScriptFile = file getUnixScript()
        unixScriptFile.text = unixScriptFile.text.replace('APP_HOME="`pwd -P`"', 'export APP_HOME="`pwd -P`"')
    }
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createZuliaDScript)
    fileMode = 0755
}

