apply plugin: 'java'

defaultTasks 'build'
subprojects {

	apply plugin: 'java'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	version = '1.1-SNAPSHOT'
	ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

	group = 'io.zulia'

	repositories {
		mavenCentral()
	}

	dependencies {
		testCompile 'org.testng:testng:6.8.21'
	}

	sourceSets {
		main {
			resources {
				srcDir 'src/main/java'
			}
		}
		test {
			resources {
				srcDir 'src/test/java'
			}
		}
	}

	test {
		useTestNG()

		systemProperty 'mongoTestConnection', 'mongodb://127.0.0.1:27017'

		//tweaking memory settings for the forked vm that runs tests
		jvmArgs '-Xmx1500m'

		beforeTest { descriptor ->
			logger.lifecycle("\n\nRunning test: " + descriptor)
		}

		afterTest { descriptor ->
			logger.lifecycle("\nFinished test: " + descriptor + "\n\n")
		}

		onOutput { descriptor, event ->
			logger.lifecycle(event.message.trim())
		}
		workingDir = 'build/'
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives sourcesJar
	}

}
