plugins {
	id "com.google.protobuf" version "0.8.2"
}

defaultTasks 'generateProto', 'build'

description 'Zulia Common'

def grpcVersion = '1.5.0'

dependencies {
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile 'org.mongodb:bson:3.5.0'
}

protobuf {

	protoc {
		artifact = 'com.google.protobuf:protoc:3.3.0'
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				// To generate deprecated interfaces and static bindService method,
				// turn the enable_deprecated option to true below:
				option 'enable_deprecated=false'
			}
		}
	}
	generatedFilesBaseDir = "$projectDir/gen"
}


clean {
	delete protobuf.generatedFilesBaseDir
}

// Inform IntelliJ projects about the generated code.
apply plugin: 'idea'

idea {
	module {
		sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
		// If you have additional sourceSets and/or codegen plugins, add all of them
		sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
	}
}

